#!/usr/bin/env bash
#*
#*                 _    __ _ _
#*  __ _ ___  __ _| |_ / _(_) |___ ___  WEBSITE: https://goatfiles.github.io
#* / _` / _ \/ _` |  _|  _| | / -_|_-<  REPOS: https://github.com/goatfiles
#* \__, \___/\__,_|\__|_| |_|_\___/__/  LICENCE: https://github.com/goatfiles/dotfiles/blob/main/LICENSE
#* |___/
#*          MAINTAINERS:
#*              AMTOINE: https://github.com/amtoine antoine#1306 7C5EE50BA27B86B7F9D5A7BA37AAE9B486CFF1AB
#*              ATXR:    https://github.com/atxr    atxr#6214    AF97503EDC65A06845114630F00A52147514670B
#*

command awk -v FS="[[:space:],]*" -v argv="$argv" '
    BEGIN {
        min = match(argv, /--min=[0-9]+/) ? substr(argv, RSTART + 6, RLENGTH - 6) + 0 : ""
        max = match(argv, /--max=[0-9]+/) ? substr(argv, RSTART + 6, RLENGTH - 6) + 0 : ""
    }
    {
        for (i = j = 1; i <= NF; i++) {
            if ($i ~ /^--/) continue
            if ($i !~ /^-?[0-9]/) data[count + j++] = ""
            else {
                v = data[count + j++] = int($i)
                if (max == "" && min == "") max = min = v
                if (max < v) max = v
                if (min > v ) min = v
            }
        }
        count += j - 1
    }
    END {
        n = split(min == max && max ? "▅ ▅" : "▁ ▂ ▃ ▄ ▅ ▆ ▇ █", blocks, " ")
        scale = (scale = int(256 * (max - min) / (n - 1))) ? scale : 1
        for (i = 1; i <= count; i++)
            out = out (data[i] == "" ? " " : blocks[idx = int(256 * (data[i] - min) / scale) + 1])
        print out
    }
'
